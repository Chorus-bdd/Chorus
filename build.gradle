
apply plugin: 'java-library-distribution'

def dependencyFile = new File("dependencies.gradle")

allprojects  {
    apply plugin: 'java'
    apply plugin: 'maven'

    repositories {
        mavenLocal()

        maven { url "http://repo.maven.apache.org/maven2" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }

    group = "org.chorusbdd"
    version = '3.0.0.DEV11'
}

subprojects {

    task updateGradleModuleDependencies {
        doLast {
            dependencyFile << "\n"
            dependencyFile << "project(':" + project.name + "') {\n"
            dependencyFile << "    dependencies {\n"

//            project.configurations.getByName("compile").allDependencies
//                    .findAll { depend -> depend.group.equals('org.chorusbdd') }
//                    .stream()
//                    .map({ dep -> dep.name })
//                    .distinct()
//                    .forEach { d -> dependencyFile << "        compile project (':" + d + "')\n" }

            //TODO deal correctly with test vs compile classpath
            project.configurations.getByName("testCompile").allDependencies
                    .findAll { depend -> depend.group.equals('org.chorusbdd') }
                    .stream()
                    .map({ dep -> dep.name })
                    .distinct()
                    .forEach { d -> dependencyFile << "        compile project (':" + d + "')\n" }

            dependencyFile << "    }\n"

            dependencyFile << "}\n"
        }
    }

}

task clearGradleModuleDependencies {
    doLast {
        dependencyFile.write "// Dependencies\n"
    }
}

task recalculateGradleDependencies

recalculateGradleDependencies.dependsOn clearGradleModuleDependencies, subprojects.updateGradleModuleDependencies

//*** MODULE DEPENDENCIES

apply from: 'dependencies.gradle'

//The dependencies from this project determine which jars get included in the
//distribution
dependencies {
    compile project ('chorus')
    compile project ('chorus-spring')
    compile project ('chorus-selenium')
    compile project ('chorus-stepserver')

    //TODO Sikuli is omitted for now. It needs different jars for different OS and so would
    //require us to create distributions with differing contents
    //compile project ('chorus-sikulix')
}

//*** END MODULE DEPENDENCIES

distributions {
    main{
        baseName = 'chorus'
    }
}

